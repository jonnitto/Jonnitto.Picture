prototype(Jonnitto.Picture:PictureTag) < prototype(Fusion:Tag) {
	tagName = 'picture'
	attributes.class = Fusion:RawArray {
		settings = ${Configuration.setting('Jonnitto.Picture.cssClass')}
	}

	images = Fusion:RawArray {
		lg = ${q(node).property('image')}
		md = ${q(node).property('laptop')}
		sm = ${q(node).property('tablet')}
		xs = ${q(node).property('mobile')}
	}

	breakpoints = ${Configuration.setting('Jonnitto.Picture.breakpoints')}
	maximumWidths = ${Configuration.setting('Jonnitto.Picture.maximumWidths')}
	maximumHeights = ${Configuration.setting('Jonnitto.Picture.maximumHeights')}
	widths = ${Configuration.setting('Jonnitto.Picture.widths')}
	heights = ${Configuration.setting('Jonnitto.Picture.heights')}

	title = ${q(node).property('title')}
	alternativeText = ${q(node).property('alternativeText')}
	caption = ${q(node).property('caption')}

	@context {
		title = ${this.title}
		alternativeText = ${this.alternativeText}
		breakpoints = ${this.breakpoints}
		maximumWidths = ${this.maximumWidths}
		maximumHeights = ${this.maximumHeights}
		widths = ${this.widths}
		heights = ${this.heights}

		sourceLG = ${this.images.lg}
		sourceMD = ${this.images.md ? this.images.md : this.images.lg}
		sourceSM = ${this.images.sm ? this.images.sm : this.images.md ? this.images.md : this.images.lg}
		sourceXS = ${this.images.xs ? this.images.xs : this.images.sm ? this.images.sm : this.images.md ? this.images.md : this.images.lg}
		fallback = ${this.images.lg ? this.images.lg : this.images.md ? this.images.md : this.images.sm ? this.images.sm : this.images.xs}
		lightbox = ${this.images.lg ? this.images.lg : this.images.md ? this.images.md : this.images.sm ? this.images.sm : this.images.xs}
		caption = ${this.caption}
	}

	@process {
		addLightbox = Jonnitto.Picture:Lightbox
		addLink = Jonnitto.Picture:Link
	}

	content = Fusion:Array {
		sourceLG = Jonnitto.Picture:Source {
			asset = ${node.context.inBackend ? false : sourceLG}
			media = ${breakpoints.lg}
			maximumWidth = ${maximumWidths.lg}
			maximumHeight = ${maximumHeights.lg}
			width = ${widths.lg}
			height = ${heights.lg}
		}
		sourceMD = Jonnitto.Picture:Source {
			asset = ${node.context.inBackend ? false : sourceMD}
			media = ${breakpoints.md}
			maximumWidth = ${maximumWidths.md}
			maximumHeight = ${maximumHeights.md}
			width = ${widths.md}
			height = ${heights.md}
		}
		sourceSM = Jonnitto.Picture:Source {
			asset = ${node.context.inBackend ? false : sourceSM}
			media = ${breakpoints.sm}
			maximumWidth = ${maximumWidths.sm}
			maximumHeight = ${maximumHeights.sm}
			width = ${widths.sm}
			height = ${heights.sm}
		}
		sourceXS = Jonnitto.Picture:Source {
			asset = ${node.context.inBackend ? false : sourceXS}
			maximumWidth = ${maximumWidths.xs}
			maximumHeight = ${maximumHeights.xs}
			width = ${widths.xs}
			height = ${heights.xs}
		}
		fallback = Jonnitto.Picture:ImageTag {
			asset = ${fallback}
			title = ${title}
			alternativeText = ${alternativeText}
			maximumWidth = ${maximumWidths.fallback}
			maximumHeight = ${maximumHeights.fallback}
			width = ${widths.fallback}
			height = ${heights.fallback}
		}
	}
}
