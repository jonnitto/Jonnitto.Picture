prototype(Helper:LinkURI) < prototype(Fusion:Value) {
	property = ${property ? property : 'link'}
	@context.read = ${q(node).property(this.property)}
	value = Fusion:Case {
		@context.linkType = Helper:LinkType {
			read = ${read}
		}
		isNode {
			condition = ${linkType == 'node'}
			renderer = NodeUri {
				@context.nodeObject = ${Neos.Link.convertUriToObject(read, node)}
				node = ${q(nodeObject).closest(Configuration.setting('Jonnitto.Headline.instanceOfDocument')).get(0)}
				section = ${q(nodeObject).is(Configuration.setting('Jonnitto.Headline.instanceOfDocument')) ? false : Configuration.setting('Jonnitto.Headline.prependIdentifier') + nodeObject.identifier}
			}
		}
		isAssetOrExternal {
			condition = ${read}
			renderer = ${read}
			renderer.@process.convertUris = ConvertUris
		}
		noLink {
			condition = true
			renderer = false
		}
	}

	@if.isValidLink = Helper:ValidLink
}
