prototype(Jonnitto.Picture:Source) < prototype(Fusion:Tag) {
	media = false
	sizes = false
	asset = false
	preset = false
	type = ${this.asset.mediatype}

	@if.hasAsset = ${this.asset ? true : false}

	maximumWidth = ${Configuration.setting('Jonnitto.Picture.maximumWidth')}
	maximumHeight = ${Configuration.setting('Jonnitto.Picture.maximumHeight')}
	width = ${Configuration.setting('Jonnitto.Picture.width')}
	height = ${Configuration.setting('Jonnitto.Picture.height')}
	allowCropping = ${Configuration.setting('Jonnitto.Picture.allowCropping')}
	allowUpScaling = ${Configuration.setting('Jonnitto.Picture.allowUpScaling')}

	@context {
		media = ${this.media}
		type = ${this.type}
		sizes = ${this.sizes}
		asset = ${this.asset}
		preset = ${this.preset}
		maximumWidth = ${this.maximumWidth}
		maximumHeight = ${this.maximumHeight}
		width = ${this.width}
		height = ${this.height}
		allowCropping = ${this.allowCropping}
		allowUpScaling = ${this.allowUpScaling}
	}

	prototype(ImageUri) {
		multiplicator = 1
		asset = ${asset}
		allowCropping = ${allowCropping}
		allowUpScaling = ${allowUpScaling}
		maximumWidth = ${width ? false : maximumWidth * this.multiplicator}
		maximumHeight = ${height ? false : maximumHeight * this.multiplicator}
		width = ${width ? width * this.multiplicator : false}
		height = ${height ? height * this.multiplicator : false}
		preset = ${this.multiplicator > 1 && preset ? preset + '.' + this.multiplicator + 'x' : preset}
		@process.addResolution = ${(this.multiplicator > 1 ? ',' : '') + value + ' ' + this.multiplicator + 'x'}
	}

	tagName = 'source'
	attributes {
		media = ${media}
		sizes = ${sizes}
		type = ${type}
		srcset = Fusion:Array {
			1 = ImageUri
			2 = ImageUri {
				multiplicator = 2
			}
		}
	}
}
